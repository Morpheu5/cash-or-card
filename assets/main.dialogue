~ start

{{customer.name}} si avvicina alla cassa. [[I suoi acquisti ammontano a|Gli articoli che ha con sé costano un totale di]] {{amount}} soldi.
if (customer.payment_method == "cash")
	% {{customer.name}}: Vorrei pagare in contanti
	% {{customer.name}}: Posso pagare in contanti?
else
	% {{customer.name}}: Vorrei pagare con la carta
	% {{customer.name}}: Posso pagare con la carta?
endif
- [[Va bene|Ok|D'accordo|Ottimo]]
	if customer.payment_method == "cash"
		do handle_payment("cash")
		do loop()
	else
		=> handle_card_payment
	endif
- Non preferisce pagare in contanti? [if customer.payment_method != "cash"]
	=> propose_cash_payment
- Non preferisce pagare con la carta? [if customer.payment_method != "card"]
	=> propose_card_payment
=> END


~ propose_card_payment
if customer.must_use_cash and customer.denial_tolerance > 0
	set customer_has_explained_conspiracy = true
	% {{customer.name}}: Assolutamente no, il denaro elettronico è un modo per renderci schiavi dei sistemi finanziari globali!
	% {{customer.name}}: Non sia ingenuo, ci vogliono controllare e negare la libertà di scelta!
	% {{customer.name}}: Bravo, così ci toglieranno il contante e saremo dipendenti dalle banche!
	% {{customer.name}}: I pagamenti elettronici sono solo uno strumento per il controllo totale delle nostre transazioni finanziarie da parte del governo e delle grandi corporazioni.
	% {{customer.name}}: I contanti garantiscono la privacy e la libertà finanziaria, mentre i pagamenti elettronici tracciano ogni singolo movimento che facciamo.
	% {{customer.name}}: Se ci affidiamo esclusivamente ai pagamenti elettronici, potremmo trovarci in una situazione di vulnerabilità totale in caso di blackout o malfunzionamenti del sistema.
	% {{customer.name}}: I contanti sono un mezzo di scambio reale, tangibile e resistente alle minacce cibernetiche, a differenza dei pagamenti elettronici che possono essere facilmente compromessi.
	% {{customer.name}}: L'uso dei contanti contribuisce a sostenere l'economia locale e i piccoli commercianti, evitando le spese delle transazioni elettroniche che incidono sul loro margine di profitto.
	% {{customer.name}}: I pagamenti elettronici mettono a rischio la nostra sicurezza finanziaria, esponendoci al rischio di frodi, hackeraggi e furti di identità.
	% {{customer.name}}: I contanti sono un diritto costituzionale che ci garantisce l'indipendenza finanziaria e la libertà di scelta.
	% {{customer.name}}: L'uso dei contanti favorisce la trasparenza nelle transazioni, evitando la creazione di un sistema finanziario completamente digitale che può essere manipolato a piacimento.
	% {{customer.name}}: I pagamenti elettronici rendono le persone dipendenti da un sistema tecnologico che potrebbe essere facilmente sottoposto a controllo o restrizioni da parte delle autorità.
	% {{customer.name}}: I contanti sono l'unica forma di denaro a corso legale, mentre i pagamenti elettronici sono soggetti a regolamenti e limitazioni imposte da terze parti che possono influenzare negativamente la nostra libertà finanziaria.
	- [[Ha ragione|Giusto|Ah, sì]], meglio usare i contanti
		do handle_payment("cash")
		do loop()
	- [[Non è proprio verissimo…|Le cose non stanno proprio così, in realtà…|A me non risulta…]] comunque [[continuo a preferire|a me fa più comodo]] la carta.
		do customer.decrement_cash_preference()
		do customer.decrement_denial_tolerance()
		=> propose_card_payment
elif customer.must_use_cash and customer.denial_tolerance == 0
	% {{customer.name}}: Vedo che è saldo nel suo supporto al NWO. Dirò a tutti di evitare questo negozio. Si tenga la sua merce!
	% {{customer.name}}: Incredibile! L'hanno plagiata proprio bene, eh? Beh, ci sarà qualcun altro da cui posso rifornirmi…
	% {{customer.name}}: Se non posso pagare in contanti, cercherò un altro negozio!
	% {{customer.name}}: Se insiste con i pagamenti elettronici, sarà bene che cercare un altro negozio che rispetti i suoi clienti!
	% {{customer.name}}: Se proprio non vuole accettare contanti, dovrò trovare altro negozio più in linea con le mie preferenze di pagamento!
	% {{customer.name}}: Mi dispiace, ma se non posso pagare in contanti, dovrò cercare altrove!
	% {{customer.name}}: Se l'uso dei contanti non è un'opzione qui, allora dovrò trovare un negozio dove sia possibile utilizzarli.
	% {{customer.name}}: Bene, andrò dove non devo dipendere dalla tecnologia per i miei acquisti!
	% {{customer.name}}: Mi piace avere il controllo fisico dei miei soldi, cercherò qualcun altro che la pensi allo stesso modo.
	% {{customer.name}}: Sono convinto che l'uso dei contanti aiuti a sostenere l'economia locale, cercherò un altro negozio in zona.
	% {{customer.name}}: Mi piace scambiare soldi reali, di sicuro ci sarà un altro negoziante a cui piace la stessa cosa.
	do handle_income_loss()
	do loop()
elif customer.prefers_cash > 0 and customer.denial_tolerance > 0
	% {{customer.name}}: Ma non ci sono commissioni altissime?
	% {{customer.name}}: Eh, ma così risparmiamo sulle commissioni!
	% {{customer.name}}: Ma così regaliamo soldi alle banche…
	% {{customer.name}}: Ma così così mi tracciano e allora addio privacy!
	% {{customer.name}}: E se poi qualcuno mi clona la carta e mi entra nel conto?
	% {{customer.name}}: Ma se non usiamo il contante finché possiamo, ce lo toglieranno del tutto!
	% {{customer.name}}: Ma lei non preferisce vedere dei soldi in cassa che un numero in banca?
	- [[Ha ragione|Giusto|Ah, sì]], meglio usare i contanti
		do handle_payment("cash")
		do loop()
	- No, [[non è proprio così]], e poi [[pagare con|usare]] la carta [[conviene|ha i suoi vantaggi|]]
		do customer.decrement_cash_preference()
		do customer.decrement_denial_tolerance()
		=> propose_card_payment
elif customer.prefers_cash == 0 and customer.denial_tolerance > 0
	% {{customer.name}}: Va bene, per questa volta pago con la carta…
	% {{customer.name}}: Ok, allora pago con la carta…
	=> handle_card_payment
	do loop()
elif customer.denial_tolerance == 0
	{{customer.name}}: Continuo a preferire i contanti, quindi le lascio la sua merce e vado altrove.
	do handle_income_loss()
	do loop()
else
	Buh
endif
=> END


~ propose_cash_payment
if customer.denial_tolerance > 0
	% {{customer.name}}: Preferisco la carta, così evito di portarmi in giro un sacco di contanti, sa com'è… la sicurezza!
	% {{customer.name}}: Eh, ma con la carta tengo meglio traccia delle spese.
	% {{customer.name}}: Ma si figuri, con la carta non dev cercare un bancomat ogni volta che devo pagare.
	% {{customer.name}}: No, la carta è meglio: se la perdo o me la rubano, non ho perso niente, mica come coi contanti…
	% {{customer.name}}: Con la carta non dobbiamo stare lì a contare i centesimini e controllare il resto…
	% {{customer.name}}: Con la carta so subito quanto ho speso e faccio prima a fare i conti a fine giornata!
	% {{customer.name}}: La carta mi dà punti o cashback, così ci guadagno io e lei non perde il cliente, no?
	% {{customer.name}}: Con la carta pago veloce, senza stare lì a contar soldi o fare la danza delle monete.
	- [[Giusto|Bene|Ottimo|Perfetto]], [[vada per la carta|eccole il terminale|pronti con la carta]]. => handle_card_payment
	- Continuo a preferire i contanti
		do customer.decrement_denial_tolerance()
		=> propose_cash_payment
	- I contanti sono l'unica forma di pagamento in corso legale
		=> legal_tender
elif customer.denial_tolerance == 0 and customer.initial_denial_tolerance >= 3
	% {{customer.name}}: E allora niente, me ne andrò da un'altra parte, magari trovo qualcuno che non evade…
	% {{customer.name}}: [[Ma guarda te|Incredibile]], accettare i pagamenti elettronici sarebbe obbligatorio ma c'è ancora gente come lei che pensa di essere al di sopra della legge. Venda questa roba a qualcun altro.
	% {{customer.name}}: [[Eh|Guarda qua]], uno prova a spingere per cambiare le cose in meglio ma c'è sempre qualcuno che rema contro. Non le spiacerà trovare un altro cliente a cui vendere questa merce.
	% {{customer.name}}: Va bene, farò [[un|due|tre]] km in più per andare dal suo concorrente, allora, rieccole la sua roba.
	do handle_income_loss()
	do loop()
elif customer.denial_tolerance == 0 and customer.initial_denial_tolerance < 3
	% {{customer.name}}: Per questa volta pago in contanti… ma spargerò la voce.
	% {{customer.name}}: OK, stavolta passi ma mi ricorderò di andare in cerca da qualche altra parte, la prossima volta.
	% {{customer.name}}: Va bene, però il suo collega di là della strada mi hanno detto che non si fa problemi con le carte…
	do handle_payment("cash")
	do loop()
endif
=> END


~ legal_tender
% {{customer.name}}: Certo, ma resta l'obbligo di accettare i pagamenti elettronici
- [[Giusto|Bene|Ottimo|Perfetto]], [[vada per la carta|eccole il terminale|pronti con la carta]]. => handle_card_payment
- Continuo a preferire i contanti
	do customer.decrement_denial_tolerance()
	=> propose_cash_payment
=> END


~ handle_card_payment
Scegli un metodo di pagamento elettronico:
- POS (commissione\: {{100 * get_fee("card", amount)}}%)
	do handle_payment("card")
	do loop()
- PayUp (commissione\: {{100 * get_fee("payup", amount)}}%)
	do handle_payment("payup")
	do loop()
- OddlyPay (commissione\: {{100 * get_fee("oddlypay", amount)}}%)
	do handle_payment("oddlypay")
	do loop()
=> END


~ ask_bank_run
A fine giornata hai {{bank}} soldi in banca e {{cash}} soldi in contanti.
- Porta i contanti in banca.
	do handle_bank_run()
- Torna a casa.
	do run()
=> END


~ end_bank_run
% E anche oggi abbiamo portato a casa la giornata.
% Bene, e ora spritz!
% Certo che è stato un po' un rischio, con tutto quel contante, ma è andata bene.
% Domani è un altro giorno!
do run()
=> END

~ street_robbery
% Ladro: In alto le mani!
% Ladro: O la borsa o la vita!
% Ladro: Svuota il portafogli!
% Ladro: Svuota le tasche!
set cash = 0.0

Sei stato derubato mentre andavi in banca! La polizia è arrivata troppo tardi per fermare il ladro e hai perso {{stolen_cash}} soldi!
- Chiama l'assicurazione (recuperi {{potential_loss}}) soldi, il premio salirà)
	do handle_insurance_claim(cash)
	=> insurance_claim_end
- Torna a casa (perdi {{stolen_cash}} soldi)
	do run()
=> END

~ shop_robbery
% Ladro: In alto le mani!
% Ladro: O la borsa o la vita!
% Ladro: Svuota il portafogli!
% Ladro: Svuota le tasche!
set cash = 0.0

Sei stato rapinato in negozio! La polizia è arrivata troppo tardi per fermare il ladro e hai perso {{stolen_cash}} soldi!
- Chiama l'assicurazione (recuperi {{potential_loss}}) soldi, il premio salirà)
	do handle_insurance_claim(cash)
	=> insurance_claim_end
- Chiudi e torna a casa (perdi {{stolen_cash}} soldi)
	do run()
=> END


~ insurance_claim_end
L'assicurazione ti restituisce {{insurance_refund}}. Il nuovo premio è {{insurance_premium}} con una franchigia di {{insurance_excess}}.
do run()

=> END


=> END!